//////////////////////
//  Lighting Input  //
//////////////////////

layout(std140, binding = SV_LIGHTS) readonly buffer LightsBuffer
{
	Light lights[];
};

struct MaterialInput
{
    sampler2D albedo;
    sampler2D normalHeight;
    sampler2D MRDAo;
    sampler2D emissive;
    float heightScale;
};
uniform MaterialInput material_input : SV_MATERIAL;

#include <texturemaps.shinc>

Material ExtractMaterial(vec2 uv, Camera camera, vec3 worldNormal, vec3 worldTangent)
{
    mat3 tbn = GetTBNMatrix(worldNormal, worldTangent);

    vec2 texcoords = ParallaxMap(material_input.normalHeight, material_input.heightScale, uv, camera, tbn);

    Material material;
    material.albedo = AlbedoMap(material_input.albedo, texcoords);
    material.normal = NormalMap(material_input.normalHeight, texcoords, tbn);
    vec4 MRDAo = LightingData(material_input.MRDAo, texcoords);
    material.metallic = MRDAo.r;
    material.roughness = MRDAo.g;
    material.dielectric = MRDAo.b;
    material.ambientOcclusion = MRDAo.a;
 	material.emissive = EmissiveMap(material_input.emissive, texcoords);
    material.F0 = Fresnel0(material.albedo, material.metallic, material.dielectric);

    return material;
}

vec3 GetAllLighting(Material material, Camera camera, vec3 worldPosition, vec3 worldNormal)
{
    vec3 lighting = vec3(0.0);

    for(int i = 0; i < lights.length(); i++)
        lighting += CalculateLight(lights[i], camera, material, worldPosition, worldNormal);

    return lighting + GetAmbientLight(material.ambientOcclusion, material.albedo.rgb) + material.emissive;
}
